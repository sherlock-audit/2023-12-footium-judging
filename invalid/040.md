Interesting Misty Puma

medium

# `FootiumGeneralPaymentContract#makePayment` emits a `PaymentMade` event that does not respect the depositor address.

## Summary

Payments made via calls to [`FootiumGeneralPaymentContract#makePayment`](https://github.com/sherlock-audit/2023-12-footium/blob/617cbc3df2fb51d9e8e5c701355efec4d4193d55/footium-eth-shareable/contracts/FootiumGeneralPaymentContract.sol#L84) emit `PaymentEvent`s to record a payment, but do not track the `msg.sender` of who originally sent the value.

## Vulnerability Detail

After satisfying pre-requisite checks when making a payment, [the `msg.value` is passed onto the `paymentReceiverAddress` and a `PaymentMade` event is emitted](https://github.com/sherlock-audit/2023-12-footium/blob/617cbc3df2fb51d9e8e5c701355efec4d4193d55/footium-eth-shareable/contracts/FootiumGeneralPaymentContract.sol#L100C9-L106C56).

With the lack of an on-chain registry for payments, presumably the `PaymentMade` event is used to inform off-chain indexing.

The `event` consists of the following properties:

```solidity
event PaymentMade(uint256 indexed clubId, uint256 indexed amount, string message);
```

However, this **does not** track the `msg.sender` responsible for sending the payment.

The implicit assumption in this system is that the `makePayment` function may only be called by the owner of the `FootiumClub`:

```solidity
 // Require that the sender is the owner of the club
if (msg.sender != footiumClub.ownerOf(_clubId)) {
    revert NotClubOwner(_clubId, msg.sender);
}
```

However, as [`FootiumClub` is a ERC721](https://github.com/sherlock-audit/2023-12-footium/blob/617cbc3df2fb51d9e8e5c701355efec4d4193d55/footium-eth-shareable/contracts/FootiumClub.sol#L15), it can be freely traded. The resulting effect is that if a club is a traded, a historical `PaymentMade` event would implicitly misrepresent the current owner as the benefactor of the payment.

## Impact

Previous `FootiumClub` holders who have made payments and since traded their clubs will not be correctly identified as the source of the payment, which may complicate off-chain handling, the execution of refunds, and surface incorrect historical information.

## Code Snippet


```solidity
/**
 * @notice Makes a payment for the given feature.
 * @param _clubId Club ID.
 * @param _message A message describing a feature to pay for.
 * @dev This function is payable.
 * Emits a {PaymentMade} event.
 */
function makePayment(uint256 _clubId, string calldata _message)
    external
    payable
    whenNotPaused
    nonReentrant
{
    // Require that the sender is the owner of the club
    if (msg.sender != footiumClub.ownerOf(_clubId)) {
        revert NotClubOwner(_clubId, msg.sender);
    }

    // Require that the payment amount is greater than 0
    if (msg.value <= 0) {
        revert IncorrectETHAmount(msg.value);
    }

    // Send the payment to the payment receiver address
    (bool sent, ) = paymentReceiverAddress.call{value: msg.value}("");
    if (!sent) {
        revert FailedToSendETH(msg.value);
    }

    emit PaymentMade(_clubId, msg.value, _message);
}
```

## Tool used

Visual Studio Code

## Recommendation

The `PaymentMade` event should be updated to define who was responsible for the payment:

```diff
- event PaymentMade(uint256 indexed clubId, uint256 indexed amount, string message);
+ event PaymentMade(uint256 indexed clubId, uint256 indexed amount, string message, address depositor);
```


